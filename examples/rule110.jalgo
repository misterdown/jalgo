st generate_tape : 0 1 0 dup<38> 1 ;

st tape_size : 40 ;
st rule110_count : tape_size ;

/* st smile_face : 2325258224 ; /* ðŸ˜Š raw value */ */
st '@' : 64 ;
st 'SP' : 32 ; /* space */

st print_tape_element : if '@' write_raw 'SP' write_raw else 'SP' write_raw 'SP' write_raw ;

st rule110_step : /* takes 3 last stack values and return to stack a new value. current[-1] | current[0] | current[1] => rule110 result */
    if
        if 
            if /* 111 */
                0
            else /* 011 */
                1
        else
            if /* 101 */
                1
            else /* 001 */
                1
    else
        if 
            if /* 110 */
                1
            else /* 010 */
                1
        else
            if /* 100 */
                0
            else /* 000 */
                0 ;

st apply_rule110 :
    /* current stack: [ tape buffer 1 pointer | tape buffer 2 pointer | tape size] */
    /* assume the tape is already initialized with a binary sequence */
    /* the head starts at the first cell */

    dup if 
        dec
        swap<0,2>
        /* current stack: [ tape size | tape buffer 2 pointer | tape buffer 1 pointer ] */

        dup<3>
        
        /* read the current cell and its two neighbors */
        8 dif read_from /* left neighbor */
        swap /* keep tape pointer on the top */

        dup read_from /* current cell */
        dup print_tape_element
        swap /* keep tape pointer on the top */

        dup 8 sum read_from /* right neighbor */
        swap
        pop
        /* apply Rule110 */
        rule110_step
        
        /* current stack: [ tape size | tape buffer 2 pointer | tape buffer 1 pointer | tape buffer 1 pointer | rule110_value ] */

        /* get tape buffer 2 pointer */
        stack_head 24 sum read_from
        /* swap with rule110_value */
        swap 
        /* write the rule110_value to the second buffer */
        write_to

        /* current stack: [ tape size | tape buffer 2 pointer | tape buffer 1 pointer | tape buffer 1 pointer ] */
        pop
        
        /* move to the next cell */
        8 sum
        swap
        8 sum
        swap
        swap<0,2>

        /* current stack: [ tape buffer 1 pointer | tape buffer 2 pointer | tape size] */
        __self__goto__
    else
        pop<3> ;

st copy_swaped : /* current: [ n1 | n2 ]. target: [n1 | n2 | n2 | n1 ] */
    dup
    stack_head 16 sum read_from ;

st apply_rule110_times : /* current stack: [ tape buffer 1 pointer | tape buffer 2 pointer | times ] */
    dup if
        dec
        swap<0,1>
        swap<1,2> /* current stack: [  times | tape buffer 1 pointer | tape buffer 2 pointer ] */
        copy_swaped /* current stack: [  times | tape buffer 1 pointer | tape buffer 2 pointer | tape buffer 2 pointer | tape buffer 1 pointer ] */
        tape_size
        apply_rule110 
        swap<0,2>
        __self__goto__
    else
        pop<3> ;


st start :
    /* initialize the tape with a binary sequence */

    generate_tape
    stack_head 8 sum /* tape buffer 1 */

    generate_tape
    stack_head 8 sum /* tape buffer 2 */

    dup                                 /* get tape buffer 2 */
    dup tape_size 8 mul 8 sum sum read_from    /* get tape buffer 1 */
    rule110_count apply_rule110_times ;